From 0354680d48da049030d7f322ab66cf34328678f3 Mon Sep 17 00:00:00 2001
From: Cao ShaoLin <slcao@ingenic.cn>
Date: Sun, 29 Jun 2014 19:05:16 +0800
Subject: [PATCH 53/58] fix java indention

---
 elisp/java-mode-indent-annotations.el | 162 ++++++++++++++++++++++++++++++++++
 rc/emacs-rc-java.el                   |   7 ++
 2 files changed, 169 insertions(+)
 create mode 100644 elisp/java-mode-indent-annotations.el

diff --git a/elisp/java-mode-indent-annotations.el b/elisp/java-mode-indent-annotations.el
new file mode 100644
index 0000000..4a37f3e
--- /dev/null
+++ b/elisp/java-mode-indent-annotations.el
@@ -0,0 +1,162 @@
+;;; java-mode-indent-annotations.el --- Indentation for Java 5 annotations.
+
+;; Copyright (C) 2006 by Suraj Acharya
+
+;; Author: Suraj Acharya <sacharya@cs.indiana.edu>
+;; Maintainer: Suraj Acharya <sacharya@cs.indiana.edu>
+;; Created: 22 Feb 2006
+;; Keywords: cc-mode java annotations indentation
+
+;; This file is not part of Emacs
+
+;; This program is free software; you can redistribute it and/or
+;; modify it under the terms of the GNU General Public License as
+;; published by the Free Software Foundation; either version 2, or (at
+;; your option) any later version.
+
+;; This program is distributed in the hope that it will be useful, but
+;; WITHOUT ANY WARRANTY; without even the implied warranty of
+;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
+;; General Public License for more details.
+
+;; You should have received a copy of the GNU General Public License
+;; along with this program; see the file COPYING.  If not, write to
+;; the Free Software Foundation, Inc., 59 Temple Place - Suite 330,
+;; Boston, MA 02111-1307, USA.
+
+
+;;; Commentary:
+;; 
+;; This library tries to indent java annotations
+;; (http://java.sun.com/j2se/1.5.0/docs/guide/language/annotations.html)
+;; like the code examples listed in the webpage.
+
+;; To use this library first ensure that
+;; java-mode-indent-annotations.el is in your load path and load it
+;; like so :
+;; (require 'java-mode-indent-annotations)
+
+;; Next, call the `java-mode-indent-annotations-setup' function to your
+;; `java-mode-hook' or `jde-mode-hook' after any calls to
+;; `c-set-style'.
+
+;; The setup function adds one of the custom indentation functions
+;; `c-single-indent-after-java-annotations' or
+;; `c-no-indent-after-java-annotations' to the offset lists of the
+;; symbols arglist-intro, topmost-intro-cont, arglist-intro,
+;; arglist-close, statement-cont and func-decl-cont.  See the
+;; documentation strings for `c-single-indent-after-java-annotations'
+;; and `c-no-indent-after-java-annotations' for more info on what they
+;; do.
+
+;;; Code:
+
+(eval-when-compile
+  (require 'cc-defs))
+
+(defun c-preprend-offset (symbol offset)
+  "Find the offset entry for SYMBOL and add OFFSET at the front of the list.
+See `c-set-offset' for a description of OFFSET and SYMBOL."
+  (let ((old-offset (cdr-safe (or (assq symbol c-offsets-alist)
+                                  (assq symbol (get 'c-offsets-alist
+                                                      'c-stylevar-fallback))))))
+    (if old-offset
+        (if (listp old-offset)
+            (c-set-offset symbol (cons offset old-offset))
+          (c-set-offset symbol (list offset old-offset)))
+      (c-set-offset symbol offset))))
+
+(defun java-mode-indent-annotations-setup ()
+  "Set up java mode indent function to handle java 1.5 annotations.
+This function should be called from a java or jde mode hook,
+after any calls to `c-set-style'."
+  (c-preprend-offset 'arglist-intro 'c-single-indent-after-java-annotations)
+  (c-preprend-offset 'topmost-intro-cont 'c-no-indent-after-java-annotations)
+  (c-preprend-offset 'arglist-close 'c-no-indent-after-java-annotations)
+  (c-preprend-offset 'statement-cont 'c-no-indent-after-java-annotations)
+  (c-preprend-offset 'func-decl-cont 'c-no-indent-after-java-annotations))
+
+(defun c-only-java-annotations-p (langelem)
+  "Check if there are only java annotations before the current line.
+It does this by moving across the region from the start of
+LANGELEM to the beginning of this line one sexp at a time.  If
+during this traversal, this function only sees whitespaces
+followed by either a '@' or a '(' then it returns t."
+  (save-excursion
+    (condition-case err ;; return nil if  any errors are thrown by forward-sexp
+        (let* ((lim (1- (c-point 'bol)))
+               (throws (catch 'notAnno
+		     (goto-char (cdr langelem))
+		     (while (< (point) lim)
+                       (if (not (looking-at "\\(\\s \\|\n\\)*\\(@\\|(\\)"))
+			   (throw 'notAnno t))
+                       (forward-sexp 1)))))
+          (if (not throws)
+              t)))))
+
+(defun c-no-indent-after-java-annotations (langelem)
+  "If preceeded by java annotations, indent this line the same as the previous.
+Works with topmost-intro-cont, statement-cont, arglist-close and func-decl-cont.
+
+With topmost-intro-cont, indents as
+@Id
+Long foo;
+
+instead of
+@Id
+<-->Long foo;
+Also for method and class declarations instead of the field foo.
+With statement-cont, indents local variables with annotations as above.
+
+With statement-cont, indents as
+@Id(
+    arg=\"value\"
+)
+Long foo;
+
+instead of
+@Id(
+    arg=\"value\"
+)
+<-->Long foo;
+
+
+With arglist-close, indents as
+@RequestForEnhancement(
+...
+)
+
+Instead of
+@RequestForEnhancement(
+...
+<-->)
+
+Argument LANGELEM The language element being indented."
+  (if (c-only-java-annotations-p langelem)
+      0))
+
+
+(defun c-single-indent-after-java-annotations (langelem)
+  "If preceeded by java annotations, indent this line more than previous.
+This function indents this line by `c-basic-offset' columns more
+than the previous line.
+
+Works with arglist-intro.
+
+Indents as
+@RequestForEnhancement(
+    id	     = 2868724,
+...
+
+instead of
+@RequestForEnhancement(
+                       id	     = 2868724,
+...
+
+Argument LANGELEM The language element being indented."
+    (if (c-only-java-annotations-p langelem)
+      c-basic-offset))
+
+(provide 'java-mode-indent-annotations)
+
+;;; java-mode-indent-annotations.el ends here
diff --git a/rc/emacs-rc-java.el b/rc/emacs-rc-java.el
index 13accb6..45de8b7 100644
--- a/rc/emacs-rc-java.el
+++ b/rc/emacs-rc-java.el
@@ -1,6 +1,13 @@
 ;; java settings begin
+
+(require 'java-mode-indent-annotations)
+
 (add-hook 'java-mode-hook
 	  '(lambda()
+	     (setq c-basic-offset 4
+		   tab-width 4
+		   indent-tabs-mode nil)
+	     (java-mode-indent-annotations-setup)
 	     (xgtags-mode 1)
 	     ))
 
-- 
2.0.4

